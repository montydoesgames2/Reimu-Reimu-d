// Unedited base game functions that should be changed in order to properly load your character.

define MISC.player1.character		= u16[0xffffff08] //literally exactly the same as global.characters, renamed for consistency
define MISC.player2.character		= u16[0xffffff0c] //used to determine the 2nd player character. (USED IN THE CUSTOM PLAYER 2 ADDON)

//to define a character, create a global variable as shown here. make sure it's set to zero!
global u8 CHARACTER_CUSTOM = 0

//check for your character by using slotnum == CHARACTER_CUSTOM (or whatever your character is defined as!)
function bool MISC.SetCharAsValid(u8 slotnum) //Determine what values are valid
{
	bool base = base.MISC.SetCharAsValid(slotnum)
	//Manual slot checking
	if slotnum == CHARACTER_CUSTOM //set this to your char number
		return true
	else if base //bases the function, checks for lower slots
		return true
	else //return false if none of these are true
		return false
}

//Function specific to TAG TEAM compatibility. basically, do you want it to be compatible with tag team? otherwise ignore this.
function bool TagTeam.SetMISCAsValid(u8 slot)
{
    bool base = base.TagTeam.SetMISCAsValid(slot)
	if (slot == CHARACTER_CUSTOM)
        return true
	else if base
		return true
    else
        return false
}

// Sets your character to valid for Blue Sphere (not relevant to special stages)
function bool MISC.SetBluSphereCharAsValid(u8 slotnum) //Determine what values are valid
{
	bool base = base.MISC.SetBluSphereCharAsValid(slotnum)
	//Manual slot checking
	if slotnum == CHARACTER_CUSTOM //set this to your char number
		return true
	else if base //bases the function, checks for lower slots
		return true
	else //return false if none of these are true
		return false
}

// Sets a unique bumper sprite for your character in the Blue Sphere title screen! (only if your character *has* bluespheres, defined in MISC.SetBluSphereCharAsValid())
function string MISC.getBlusphereBumpers(u8 char)
{
	if char == CHARACTER_CUSTOM
		return "bump_custom"
	else
		return base.MISC.getBlusphereBumpers(char)
}

//to set your char, simply replace CHARACTER_CUSTOM with your character's variable! if you're defining more than one, just duplicate the code for however many there are.
function void MISC.SetChars(u8 val)
{
	val += 1
	CHARACTER_CUSTOM = val
	
	base.MISC.SetChars(val)
}

// Make sure the two functions below are correctly correlated, problems will occur otherwise (primarily with saving)
function string MISC.GetStringFromChar(u8 char)
{
	if char == CHARACTER_CUSTOM
		return "CHARACTER_CUSTOM"
	else
		return base.MISC.GetStringFromChar(char)
}

function u8 MISC.GetCharFromString(string char)
{
	if char == "CHARACTER_CUSTOM"
		return CHARACTER_CUSTOM
	else
		return base.MISC.GetCharFromString(char)
}

// These functions determine the base character of either the first OR 2nd player depending on how code applies it.
function u8 MISC.getCharBase(u8 character)
{
	if character == CHARACTER_CUSTOM
		return CHARACTER_SONIC
	return base.MISC.getCharBase(character)
}

// Necessary for Second Player MISC support
function u8 MISC.GetPartner(u8 character)
{
	u8 slotnum = character - 1
	if slotnum == CHARACTER_CUSTOM
		return 0xff //make this CHARACTER_TAILS if you want your char to have tails, you can also make this CHARACTER_KNUCKLES or CHARACTER_SONIC but unless you have custom player 2s, this will do nothing.
	return base.MISC.GetPartner(character)
}

// Changes the lives icon for HUD
function string getCharacterLivesIcon(u8 character)
{
	// example, change to your own slot
	if global.characters == CHARACTER_CUSTOM
		return "hud_lives_icon_char4"
	return base.getCharacterLivesIcon(character)
}

// Changes continues icon for Special Stage Results
function string getCharacterContinueIcon(u8 character)
{
	// example, change to your own slot
	// Special handling for active extra character
	if global.characters == CHARACTER_CUSTOM
		return "continue_icon_char4"
	
	 // Call the base function
	return base.getCharacterContinueIcon(character)
}

// Changes name sprite for level results and Special Stage results
// "SUPER" name sprite and "HYPER" name sprite add "_super" and "_hyper" onto the end of your sprite key, respectively.
function string getCharacterResultsNameplate(u8 character)
{
	// example, change to your own slot
	if global.characters == CHARACTER_CUSTOM
		return "result_nameplate_char4"
	return base.getCharacterResultsNameplate(character)
}

// Changes the sprite key for primary character (replacing the one corresponding to your character base) on the signpost
function string getCharacterSignpostSpriteKey(u8 character)
{
	if (character == CHARACTER_CUSTOM)
	{
		return "signpost_char4"
	}
	
	// Call the base function
	return base.getCharacterSignpostSpriteKey(character)
}

// Changes sprite keys for character endposes at their respective ends of the game
function string getCharacterEndPoseSpriteKey(u8 character, bool isSuperActive)
{
	if character == CHARACTER_CUSTOM
		return stringformat((isSuperActive) ? "endpose_char%s_super" : "endpose_char%s", character)
		//load seperate super/base endpose sprites. super is endpose_char4_super, base is endpose_char4
	return base.getCharacterEndPoseSpriteKey(character, isSuperActive)
}

// ESS compat. make sure the u64 in ESS_getcharname matches the name checks in ESS_getCharLifeIcon and ESS_getCharContIcon
function u64 ESS_getcharname(u8 chars)
{
	u8 slotnum = chars - 1
	if slotnum ==  CHARACTER_CUSTOM
		return "CUSTOM"
	else
		return base.ESS_getcharname(chars)
}

function u64 ESS_getCharLifeIcon(u64 name)
{
	if (name == "CUSTOM")
		return "ESS_Life_Custom"
	return base.ESS_getCharLifeIcon(name)
}

function u64 ESS_getCharContIcon(u64 name)
{
	if (name == "CUSTOM")
		return "ESS_Cont_Custom"
		//replace with return "yourconticon"
	return base.ESS_getCharContIcon(name)
}

// Specific to MISC for convenience. Modify to include your character's palette(s).
function string getMISCPaletteKey(u8 character)
{
	if (character == CHARACTER_CUSTOM)
    {
		return "character_palette_custom"
    }
	return base.getMISCPaletteKey(character)
}

// for more complex palette loading criteria, check AdvancedFunctions.

// Modify to include your character's Blue Sphere special stage sprites
function string getCharacterBluesphereSpriteKey(u8 character)
{
	if (global.characters == CHARACTER_CUSTOM)
	{
		return "bluesphere_char4"
	}
	return base.getCharacterBluesphereSpriteKey(character)
}

// Modify to include your character's Blue Sphere special stage palette
function string getCharacterBluespherePaletteKey(u8 character)
{
	if (global.characters == CHARACTER_CUSTOM)
	{
		return "bluesphere_palette_char4"
	}
	return base.getCharacterBluespherePaletteKey(character)
}

// Animated continue icons for continue screen (for how many continues you have left)
function string MISC.getContinueIcons(u8 frame) //frames go between 0 and 1
{
	if (global.characters == CHARACTER_CUSTOM)
	{
		return stringformat("continue_icon_charcustom_%d", frame)
	}
}

// Character sprites for their continue animations
function string MISC.getContinueSprites(u8 frame) //frames go between 0 and 1
{
	if (global.characters == CHARACTER_CUSTOM)
	{
		return stringformat("character_charcustom_continue_%d", frame)
	}
}

// for special continue sprites SPECIFIC TO HAVING A PARTNER, check AdvancedFunctions.lemon

// GetCharSpriteKey functions to be used in tandem with stuff in RenderChar.lemon
function string MISC.GetCharSpriteKey(u8 animationSprite)
{
	return MISC.GetCharSpriteKey(animationSprite, global.characters)
}

function string MISC.GetCharSpriteKey(u8 animationSprite, u8 character)
{
	if character == CHARACTER_CUSTOM
	{
		return stringformat("character_charcustom_0x%02x", animationSprite) //replace with your sprite key!
	}
	return base.MISC.GetCharSpriteKey(animationSprite, character)
}

// Used for Discord statuses for your character's name, by default uses the same name as Act Select
function string MISC.GetCharNameDiscordRPC(u8 character)
{
	if (character != CHARACTER_CUSTOM)
		return base.MISC.GetCharNameDiscordRPC(character)
	return MISC.GetCharName(character+1)
}

// Used for your character's name in Act select. and probably at least one other thing (Contributed by AtomicRay)
function string MISC.GetCharName(u8 character)
{
	u8 slotnum = (global.game_mode == 0x2c) ? character : (character - 1)
	if slotnum == CHARACTER_CUSTOM
		return "GETYOUROWNDAMNNAME" //name
	
	return base.MISC.GetCharName(character)
}

// Default way of rendering characters in Act Select
function string MISC.GetActSelSprite(u8 character)
{
	u8 slotnum = (global.game_mode == 0x2c) ? character : (character - 1)
	if slotnum == CHARACTER_CUSTOM
		return "ActSel_charcustom" //sprite key
		
	return base.MISC.GetActSelSprite(character)
}

// Default way of rendering a character's partner in Act Select if they have one
function string MISC.getActSelectPartnerKey(u8 character)
{
	u8 slotnum = (global.game_mode == 0x2c) ? character : (character - 1)
	if slotnum == CHARACTER_CUSTOM //replace with your char
	{
		//you CAN render anyone here with a different sprite key. e.g. "ActSel_Ashura"
		return 0 //but by default, just render tails.
	}
		
	return base.MISC.getActSelectPartnerKey(character)
}

// Renders the lives & continues icons in the vanilla Data Select
function void MISC.drawLifeContSprite(s16 px, s16 py, u16 renderQueue, u8 slotnum)
{
	if (slotnum == CHARACTER_CUSTOM) // Character selection AFTER Knuckles & Tails, sets new icon sprites
	{
		string key = "dataSel_lifeContinue_charcustom" //replace with your sprite
		Renderer.drawCustomSprite(key, px - 18, py + 0x08, 0x80, SPRITE_FLAG_PRIO, renderQueue)
	}
	else
		base.MISC.drawLifeContSprite(px, py, renderQueue, slotnum)
}

// Renders your character's sprite in vanilla Data Select
function void MISC.drawCharSprite(s16 px, s16 py, u16 renderQueue, u8 slotnum)
{
	if (slotnum == CHARACTER_CUSTOM)
	{
		string key = "dataSel_charcustom" //replace with your char sprite
			
		//use this to add a partner if you want. KEEP IN MIND that if you want partners, you need to -6 from your base char's px.
		//Also, you can render anyone here. just use the correct sprite key in place of tails'
		if MISC.GetPartner(slotnum + 1) != 0xff //if you don't intend on adding a partner tails, this literally will never be true.
		{
			Renderer.drawCustomSprite(key, px-6, py, 0x80, SPRITE_FLAG_PRIO, renderQueue) //The regular char
			Renderer.drawCustomSprite(getCharacterSelectSpriteKey(CHARACTER_TAILS), px+6, py-18, 0x80, SPRITE_FLAG_PRIO | SPRITE_FLAG_FLIP_X, renderQueue) //Tails (or your partner)
		}
		else
		{
			Renderer.drawCustomSprite(key, px, py, 0x80, SPRITE_FLAG_PRIO, renderQueue)
		}
	}
	else
		base.MISC.drawCharSprite(px, py, renderQueue, slotnum)
}

#if GAMEAPP >= 0x24060180

// Renders your character's sprite in Act Select if you are using a Test Build that has it
function void MISC.drawActSelCharSprite(s16 px, u8 levelselect.characters)
{
	u8 slotnum = levelselect.characters - 1
	
	if (slotnum == CHARACTER_CUSTOM)
	{
		if MISC.GetPartner(levelselect.characters) != 0xff //again, will never be true if you don't set a partner
			{
			Renderer.drawSprite(ActSelect.GetCharacterCombinationSprite(levelselect.characters), px - 8, 156, 0, 0x00 | SPRITE_FLAG_FLIP_X, 0xf004) //the regular character
			string key = MISC.getActSelectPartnerKey(levelselect.characters-1) //remember the partner key you set earlier? Here it is!
			
			if (!Renderer.hasCustomSprite(key)) //default to tails as a failsafe
				key = ActSelect.GetCharacterCombinationSprite(2)
				
			Renderer.drawSprite(key, px + 8, 154, 0, SPRITE_FLAG_FLIP_X, 0xf004) //draw partner character
			}
		else
			{
			Renderer.drawSprite(ActSelect.GetCharacterCombinationSprite(levelselect.characters), px, 156, 0, 0x00, 0xf004)
			}
	}
	else
		base.MISC.drawActSelCharSprite(px, levelselect.characters)
}

#endif

// Render portraits for completed saves in vanilla Data Select (contributed by nabbup)
function string MISC.getClearPortrait(u8 ending, u8 slotnum)
{
	// ending represents if they beat the game with all Chaos/Super emeralds or not.
	// 0: incomplete, 1: without emeralds, 2: all chaos emeralds, 3: all super emeralds

	if (slotnum != CHARACTER_CUSTOM || ending == 0)
		return base.MISC.getClearPortrait(ending, slotnum)
	string key
	// the following keys are just examples, and can really just be whatever,
	// however center values for the sprites MUST be "Center": "40,120"
	if (ending == 1)
		key = "dataSel_savePortrait_char4"
	else if (ending == 2)
		key = "dataSel_savePortrait_superchar4"
	else if (ending == 3)
		key = "dataSel_savePortrait_clear_char4"
	if (Renderer.hasCustomSprite(key))
		return key
	return false
}

// What zones do you want your character to access?
function u8 MISC.getAllowedZones(u8 slotnum)
{
    // add exceptions for your individual character like this:
    // if (slotnum == CHARACTER_CUSTOM)
    //    return 0x0d
	// 
	// for reference:
	// 0x0d - Sonic zones (includes doomsday)
	// 0x0c - Tails zones (excludes doomsday)
	// 0x0b - Knux zones (ends at ssz)
	//
    // return base.MISC.getAllowedZones(slotnum)
    if (slotnum == CHARACTER_CUSTOM)
	{
		return 0x0d
	}
	return base.MISC.getAllowedZones(slotnum)
}

define MISC.dataSelBackup = u16[A1 + 0x34]

//Compatibility for accessing FBZ with Useott's Original Zone Order
function bool UseottOZO.allowFBZWhileSelecting()
{
	if (MISC.dataSelBackup != CHARACTER_CUSTOM)
		return base.UseottOZO.allowFBZWhileSelecting()
	return true
}

function bool UseottOZO.allowFBZForZoneNumbers()
{
	u8 slotnum = MISC.GetCharFromString(MISC.GetStringFromSlot(dataselect.slot_selected))
	if (slotnum != CHARACTER_CUSTOM)
		return base.UseottOZO.allowFBZForZoneNumbers()
	return true
}

//For more complex zone order manipulations, return false on the UOZO functions and look at AdvancedFunctions.lemon

// In case you want to change something with the way ESS switches the zone selection
// D6 handles the maximum, apparently
function void ESS_switchZoneSelection()
{
	u8 slotnum = MISC.GetCharFromString(MISC.GetStringFromSlot(dataselect.slot_selected))
	if (slotnum == CHARACTER_CUSTOM)
		D6 = MISC.getAllowedZones(slotnum)
	base.ESS_switchZoneSelection()
}

// Determines if AI Tails should spawn in levels for transitions e.g. MHZ1 and CNZ1 intros as Sonic
// this can be useful to edit if your character has a custom method of entering the stage, like a custom intro.
// for most cases this should be true, unless your char uses knuckles' zones.

function bool SpawnAITails(u8 slotnum)
{
	if slotnum == CHARACTER_CUSTOM //replace with your char number
		return true //true means tails carries you into cnz and mhz. false means you just fall in.
	else
		return base.SpawnAITails(slotnum)
}

//for more in-depth tweaks to character level transitions, check AdvancedFunctions.lemon

// Chooses pilot for normal scale, in ending
// For both this and the small one, the Knuckles version of Sonic's head uses a separate palette line
function void MISC.getPilot(u8 slotnum)
{
	if slotnum != CHARACTER_CUSTOM
	{
		base.MISC.getPilot(slotnum)
		return
	}

	// For MISC by default: Put Tails into the Tornado
	fn05eb72()

	// sonic in tornado (tails version)
	//fn05eb72()

	// alt sonic (knuckles version)
	//fn05eb9e()
}

// Chooses pilot for smaller scale
function u8 MISC.getSmallPilot(u8 slotnum)
{
	if slotnum != CHARACTER_CUSTOM
		return base.MISC.getSmallPilot(slotnum)

	//use numbers to determine which pilot
	return 1
	// 1 - tails
	// 2 - sonic
	// 3 - sonic (knuckles' version)
}

// Change to a sprite key for the smallest version of your character riding the Tornado in the ending
function string MISC.getMiniChar(u8 slotnum)
{
	if slotnum != CHARACTER_CUSTOM
		return base.MISC.getMiniChar(slotnum)

	return "MiniChar4" //replace with your slot
}

//s3c sound test/level select compatibility
function bool LevelSelect.innerUpdate()
{
	u8 slotnum = levelselect.characters - 1
	if Mods.isModActive("Sonic 3 Complete Level Select+")
	{
		if (slotnum == CHARACTER_CUSTOM)
		{
			Renderer.drawCustomSprite("s3c_char4", getScreenExtend() + 255, 115, 0x00, 0, 0x2000) //replace with your sprites
		}
	}
	return base.LevelSelect.innerUpdate()
}

// Best Ending stuff
// Determines sprite keys for your character in the best ending (shows up alongside the base 3 characters by default)
function string MISC.getEndLogoSprites(u8 animation)
{
	if (global.characters != CHARACTER_CUSTOM) // Your character here
		return base.MISC.getEndLogoSprites(animation)
	
	return stringformat("endlogo_char4_0x%02x", animation) // Replace char4 with your slot
}

// for advanced animation timings, look at AdvancedFunctions.lemon

// Changes monitor icons for your character
function u64 Monitor.getIconSpriteKey(u8 type)
{
	if (global.characters != CHARACTER_CUSTOM)
		return base.Monitor.getIconSpriteKey(type)
		
	// Any of these can be removed to run through other mods for sprite keys
	if (type == 0)
		return "monitor_icon_static"
	else if (type == 0x01)
		return "monitor_icon_charCUSTOM" // just replace this with the key for your character's monitor icon
	else if (type == 0x02)
		return "monitor_icon_robotnik"
	else if (type == 0x03)
		return "monitor_icon_ring"
	else if (type == 0x04)
		return "monitor_icon_speedshoes"
	else if (type == 0x05)
		return "monitor_icon_fireshield"
	else if (type == 0x06)
		return "monitor_icon_lightningshield"
	else if (type == 0x07)
		return "monitor_icon_bubbleshield"
	else if (type == 0x08)
		return "monitor_icon_invincibility"
	else if (type == 0x09)
		return "monitor_icon_super"
	else if (type == 0x0b)
		return "monitor_icon_classicshield"

	return base.Monitor.getIconSpriteKey(type)
}

// Changes the little fire icon at the end of every line of the score tally for your character
function string getCharacterBonusTextIcon(u8 character)
{
	if (global.characters == CHARACTER_CUSTOM)
		return "hud_bonus_icon_char_custom"
	return base.getCharacterBonusTextIcon(character)
}

// In order to use the hitbox functions below, put MISC.SetCharHitbox(1) in the Character.Initialization.shared() function.
// This is already set up in the Special 2nd player addon, and in TAG team, so you could reference the code for that if you want to use these functions.

// Keep in mind that this can cause bugs in some interactions relating to size, because SONIC 3 SUCKS- so you may have to do some debugging after.

function u8 MISC.GetPlayerHeight(u8 character)
{
	if character == CHARACTER_CUSTOM
	{
		return char.hitbox.y.UPRIGHT
	}
	
	return base.MISC.GetPlayerHeight(character)
}

function void MISC.SetCharHitbox(u8 player)
{
	if MISC.player == CHARACTER_CUSTOM
	{
		//Log = 1
		u32 A0backup = A0
		A0 = (player == 1) ? 0xffffb000 : (player == 2) ? 0xffffb04a : (player == 3) ? 0xffffc2ca : 0xffffc314
		u8 characterHeight = MISC.GetPlayerHeight(MISC.player)
		char.hitbox.default.x = char.hitbox.x.UPRIGHT
		char.hitbox.default.y = characterHeight
		
		if char.hitbox_extends.x != char.hitbox.x.ROLLING
		{
			if (global.zone_act == 0x0300) && checkpoint.number == 0 && SpawnAITails(global.characters) // fix for cnz
				char.hitbox_extends.y = char.hitbox.y.UPRIGHT
			else
				char.hitbox_extends.y = characterHeight
			char.hitbox_extends.x = char.hitbox.x.UPRIGHT
		}
		A0 = A0backup
		return
	}
	
	base.MISC.SetCharHitbox(player)
}